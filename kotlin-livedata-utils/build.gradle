plugins {
	id 'com.jfrog.bintray'
	id 'org.shipkit.bintray'
	id 'com.diffplug.gradle.spotless' version '3.13.0'
	id 'com.github.ben-manes.versions' version '0.17.0'
	id "com.dorongold.task-tree" version '1.3'
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'digital.wup.android-maven-publish'

android {
	compileSdkVersion 27

	defaultConfig {
		minSdkVersion 14
		targetSdkVersion 27
		versionCode 1
		versionName project.version

		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

group 'com.github.magneticflux'

publishing {
	publications {
		kotlinLiveDataUtils(MavenPublication) {
			from components.android
			//noinspection GroovyAssignabilityCheck
			artifact sourcesJar
			//noinspection GroovyAssignabilityCheck
			artifact dokkaJavadocJar
		}
	}
}

dokka {
	outputFormat = 'javadoc'
	jdkVersion = 7
	impliedPlatforms = ["JVM"]
}

task dokkaJavadocJar(type: Jar, dependsOn: dokka) {
	classifier = 'javadoc'
	//noinspection GroovyAssignabilityCheck
	from dokka.outputDirectory
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from android.sourceSets.main.java.srcDirs
}

spotless {
	kotlin { ktlint('0.23.1') }

	groovyGradle { greclipse() }
}

tasks.withType(Test) { useJUnitPlatform() }

dependencies {
	// Kotlin
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

	// LiveData
	implementation "android.arch.lifecycle:livedata:$lifecycle_version"
	testImplementation "android.arch.core:core-testing:$lifecycle_version"

	// Hamcrest
	testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'

	// JUnit
	testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
}