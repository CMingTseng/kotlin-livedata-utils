plugins {
	id 'com.jfrog.bintray'
	id 'org.shipkit.bintray'
	id 'com.diffplug.gradle.spotless' version '3.13.0'
	id 'se.patrikerdes.use-latest-versions' version '0.2.3'
	id 'com.github.ben-manes.versions' version '0.20.0'
	id "com.dorongold.task-tree" version '1.3'
	id "net.ltgt.errorprone" version "0.0.14"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'digital.wup.android-maven-publish'

android {
	compileSdkVersion 28

	defaultConfig {
		minSdkVersion 14
		targetSdkVersion 28
		versionCode 1
		versionName project.version

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	flavorDimensions "support-version"

	productFlavors {
		android { dimension "support-version" }
		androidx { dimension "support-version" }
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

group 'com.github.magneticflux'

publishing {
	publications {
		kotlinLiveDataUtilsAndroid(MavenPublication) {
			from components.androidAndroidRelease

			artifactId 'kotlin-livedata-utils-android-support'

			//noinspection GroovyAssignabilityCheck
			artifact sourcesJar
			//noinspection GroovyAssignabilityCheck
			artifact dokkaJavadocJar
		}
		kotlinLiveDataUtilsAndroidX(MavenPublication) {
			from components.androidAndroidxRelease

			artifactId 'kotlin-livedata-utils-androidx'

			//noinspection GroovyAssignabilityCheck
			artifact sourcesJar
			//noinspection GroovyAssignabilityCheck
			artifact dokkaJavadocJar
		}
	}
}

dokka {
	outputFormat = 'javadoc'
	jdkVersion = 7
	impliedPlatforms = ["JVM"]
}

task dokkaJavadocJar(type: Jar, dependsOn: dokka) {
	classifier = 'javadoc'
	//noinspection GroovyAssignabilityCheck
	from dokka.outputDirectory
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from android.sourceSets.main.java.srcDirs
}

spotless {
	kotlin { ktlint('0.24.0') }

	groovyGradle { greclipse() }
}

tasks.withType(Test) { useJUnitPlatform() }

dependencies {
	// Kotlin
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

	// LiveData AndroidX
	androidxImplementation 'androidx.lifecycle:lifecycle-livedata:2.0.0-beta01'
	testAndroidxImplementation 'androidx.arch.core:core-testing:2.0.0-beta01'

	// LiveData Android
	androidImplementation 'android.arch.lifecycle:livedata:1.1.1'

	// Hamcrest
	testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'

	// JUnit
	testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
}